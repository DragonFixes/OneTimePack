plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version libs.versions.shadow apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.github.johnrengelman.shadow'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        if (project.name != 'common') {
            implementation project(':common')
        }
        compileOnly 'org.jetbrains:annotations:24.0.1'
        compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    jar {
        if (project.name.startsWith('bungeecord-') || project.name.startsWith('velocity-')) {
            dependsOn (shadowJar)
        }
    }

    shadowJar {
        archiveBaseName.set(rootProject.name + '-' + project.name)
        archiveClassifier.set('')
        destinationDirectory.set(file(rootProject.layout.buildDirectory.dir('libs')))
        minimize()
    }
}

jar {
    enabled = false
}